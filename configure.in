# Process this file with autoconf to produce a configure script.
AC_INIT(Makefile.in)
AC_CONFIG_HEADER(src/ares/config.h)

# Checks for programs

# Checks for libraries
AC_CHECK_LIB(pthread, pthread_create)

# Checks for header files
AC_HEADER_STDC
AC_CHECK_HEADERS(hash_map)
AC_CHECK_HEADERS(libintl.h)
AC_CHECK_HEADERS(pthread.h)
AC_CHECK_HEADERS(sys/epoll.h)

# Checks for typedefs, structures, and compiler characteristics
#AC_CHECK_TYPE(socklen_t, int)

# Checks for library functions
AC_CHECK_FUNCS(getaddrinfo getnameinfo)
AC_CHECK_FUNCS(inet_pton inet_ntop)
AC_CHECK_FUNCS(select poll epoll_create)
AC_CHECK_FUNCS(usleep)
AC_CHECK_FUNCS(gettext)

# Decide whether to build a shared or a static library
AC_MSG_CHECKING(whether to build a shared library)
AC_ARG_ENABLE(shared,
  AC_HELP_STRING([--enable-shared], [build a shared library (default is YES)]),
  [shared=$enableval],
  [shared="yes"])

if test "$shared" != "no"; then
 shared="yes"
fi

AC_MSG_RESULT($shared)
AC_SUBST(shared)

# Choose build variant
AC_MSG_CHECKING(for debug mode)
AC_ARG_ENABLE(debug,
  AC_HELP_STRING([--enable-debug], [build with debugging symbols]),
  [debug_enabled="yes"],
  [debug_enabled="no"])
AC_MSG_RESULT($debug_enabled)

AC_MSG_CHECKING(for hardcore trace mode)
AC_ARG_ENABLE(trace,
  AC_HELP_STRING([--enable-trace],
  [build with hardcore tracing enabled (implies --enable-debug)]),
  [trace_enabled="yes"],
  [trace_enabled="no"])
AC_MSG_RESULT($trace_enabled)

if test "$trace_enabled" != "no"; then
  AC_DEFINE(ARES_TRACE_ENABLED, 1, [ ])
  variant="trace"
elif test "$debug_enabled" != "no"; then
  variant="debug"
else
  variant="release"
fi

AC_SUBST(variant)

# Check for CPPUnit
AC_MSG_CHECKING(for CPPUnit support)
AC_ARG_WITH(cppunit,
  AC_HELP_STRING([--with-cppunit=DIR],
                 [specify path to CPPUnit (default DIR is /usr/local)]),
  [cppunit=$withval],
  [cppunit=no])
AC_MSG_RESULT($cppunit)
AC_SUBST(cppunit)

if test "$cppunit" != "no"; then
  if test "$cppunit" = "yes"; then
    cppunit="/usr/local"
  fi
  if test ! -d "$cppunit/include/cppunit"; then
    AC_MSG_ERROR(couldn't find CPPUnit in $cppunit)
  fi
fi

# Generate files and clean up
AC_SUBST(prefix)
AC_OUTPUT(Makefile)
